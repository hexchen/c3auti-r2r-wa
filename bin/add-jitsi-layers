#!/usr/bin/jq -Mf

. |
    1293 as $jitsi_tile |
    16 as $room_width |
    16 as $room_height |
    [
        "a2", "a5", "a6", "a7",
        "b1", "b3", "b4", "b5", "b7", "b8",
        "c2", "c3", "c5", "c8",
        "d1", "d2", "d3", "d6", "d8",
        "e1", "e6", "e7",
        "f1", "f3", "f5", "f6", "f7",
        "g2", "g4", "g7", "g8",
        "h1", "h2", "h3", "h4", "h5", "h6", "h8"
    ] as $jitsi_rooms |
    reduce $jitsi_rooms[] as $room (.;
        ("abcdefgh" | split("") | bsearch($room[0:1])) as $rx |
        ($room[1:2] | 8 - tonumber ) as $ry |
        [
            ($ry * $room_height + range($room_height)) * .width
                + ($rx * $room_width + range($room_width))
        ] as $room_indices |
        .layers = [
            {
                "data": (
                    [range(.height*.width)|0] | .[$room_indices[]] = $jitsi_tile
                ),
                "height":.height,
                "id":.nextlayerid,
                "name":("jitsi-"+$room),
                "opacity":1,
                "properties": [
                    {
                        "name":"jitsiRoom",
                        "type":"string",
                        "value":$room
                    }
                ],
                "type":"tilelayer",
                "visible":true,
                "width":.width,
                "x":0,
                "y":0
            }
        ] + .layers |
        .nextlayerid += 1
    )
